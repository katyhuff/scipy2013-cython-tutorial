{
 "metadata": {
  "name": "cython-julia-set"
 },
 "nbformat": 3,
 "nbformat_minor": 0,
 "worksheets": [
  {
   "cells": [
    {
     "cell_type": "code",
     "collapsed": false,
     "input": [
      "%load_ext cythonmagic"
     ],
     "language": "python",
     "metadata": {},
     "outputs": []
    },
    {
     "cell_type": "code",
     "collapsed": false,
     "input": [
      "import numpy as np\n",
      "from time import time\n",
      "\n",
      "def kernel(z, c, lim, cutoff=1e6):\n",
      "    ''' Computes the number, `n`, of iterations necessary such that \n",
      "    |z_n| > `lim`, where `z_n = z_{n-1}**2 + c`.\n",
      "    '''\n",
      "    count = 0\n",
      "    while abs(z) < lim and count < cutoff:\n",
      "        z = z * z + c\n",
      "        count += 1\n",
      "    return count\n",
      "\n",
      "\n",
      "def compute_julia(c, N, bound=2, lim=1000., kernel=kernel):\n",
      "    ''' Pure Python calculation of the Julia set for a given `c`.  No NumPy\n",
      "    array operations are used.\n",
      "    '''\n",
      "    julia = np.empty((N, N), dtype=np.uint32)\n",
      "    grid_x = np.linspace(-bound, bound, N)\n",
      "    grid_y = grid_x * 1j\n",
      "    c = complex(c)\n",
      "    t0 = time()\n",
      "    for i, x in enumerate(grid_x):\n",
      "        for j, y in enumerate(grid_y):\n",
      "            julia[i,j] = kernel(x+y, c, lim)\n",
      "    return julia, time() - t0"
     ],
     "language": "python",
     "metadata": {},
     "outputs": []
    },
    {
     "cell_type": "code",
     "collapsed": false,
     "input": [
      "import pylab as pl\n",
      "\n",
      "def plot_julia(kwargs, compute_julia):\n",
      "    ''' Given parameters dict in `kwargs` and a function to compute the Julia\n",
      "    set (`compute_julia`), plots the resulting Julia set with appropriately\n",
      "    labeled axes.\n",
      "    '''\n",
      "    kwargs = kwargs.copy()\n",
      "\n",
      "    def _plotter(kwargs):\n",
      "        bound = kwargs['bound']\n",
      "        julia, time = compute_julia(**kwargs)\n",
      "        print \"execution time (s):\", time\n",
      "        julia = np.log(julia)\n",
      "        pl.imshow(julia, \n",
      "                  interpolation='nearest',\n",
      "                  extent=(-bound, bound)*2)\n",
      "        pl.colorbar()\n",
      "        title = r\"Julia set for $C={0.real:5.3f}+{0.imag:5.3f}i$ $[{1}\\times{1}]$\"\n",
      "        pl.title(title.format(kwargs['c'], kwargs['N']))\n",
      "        pl.xlabel(\"$Re(z)$\")\n",
      "        pl.ylabel(\"$Im(z)$\")\n",
      "\n",
      "    pl.figure(figsize=(14, 12))\n",
      "\n",
      "    cvals = [0.285+0.01j, -0.1+0.651j, -0.4+0.6j, -0.8+0.156j]\n",
      "    subplots = ['221',    '222',       '223',     '224'      ]\n",
      "\n",
      "    for c, sp in zip(cvals, subplots):\n",
      "        kwargs.update(c=c)\n",
      "        pl.subplot(sp)\n",
      "        _plotter(kwargs)\n",
      "\n",
      "    pl.show()"
     ],
     "language": "python",
     "metadata": {},
     "outputs": []
    },
    {
     "cell_type": "code",
     "collapsed": false,
     "input": [
      "kwargs = dict(N=100, bound=1.5)\n",
      "plot_julia(kwargs, compute_julia)"
     ],
     "language": "python",
     "metadata": {},
     "outputs": []
    },
    {
     "cell_type": "code",
     "collapsed": false,
     "input": [
      "%%cython\n",
      "\n",
      "from time import time\n",
      "\n",
      "import numpy as np\n",
      "cimport cython\n",
      "cimport numpy as cnp\n",
      "\n",
      "ctypedef double complex cpx_t\n",
      "ctypedef double         real_t\n",
      "\n",
      "cdef inline real_t cabs_sq(cpx_t z) nogil:\n",
      "    ''' Helper inline function, computes the square of the abs. value of the\n",
      "    complex number `z`.\n",
      "    '''\n",
      "    return z.real * z.real + z.imag * z.imag\n",
      "    \n",
      "cpdef unsigned int kernel(cpx_t z, \n",
      "                                 cpx_t c,\n",
      "                                 real_t lim,\n",
      "                                 real_t cutoff=1e6) nogil:\n",
      "    ''' Cython implementation of the kernel computation.\n",
      "\n",
      "    This is implemented so that no C-API calls are made inside the function\n",
      "    body.  Even still, there is some overhead as compared with a pure C\n",
      "    implementation.\n",
      "    '''\n",
      "    cdef unsigned int count = 0\n",
      "    cdef real_t lim_sq = lim * lim\n",
      "    while cabs_sq(z) < lim_sq and count < cutoff:\n",
      "        z = z * z + c\n",
      "        count += 1\n",
      "    return count\n",
      "\n",
      "@cython.boundscheck(False)\n",
      "@cython.wraparound(False)\n",
      "def compute_julia_opt(cpx_t c,\n",
      "                       unsigned int N,\n",
      "                       real_t bound=1.5,\n",
      "                       real_t lim=1000.):\n",
      "    '''\n",
      "    Cython `compute_julia()` implementation with Numpy array buffer\n",
      "    declarations and appropriate compiler directives.  The body of this\n",
      "    function is nearly identical to the `compute_julia_no_opt()` function.\n",
      "\n",
      "    '''\n",
      "\n",
      "    cdef cnp.ndarray[cnp.uint32_t, ndim=2, mode='c'] julia \n",
      "    cdef cnp.ndarray[real_t, ndim=1, mode='c'] grid\n",
      "    cdef unsigned int i, j\n",
      "    cdef real_t x, y\n",
      "\n",
      "    julia = np.empty((N, N), dtype=np.uint32)\n",
      "    grid = np.linspace(-bound, bound, N)\n",
      "    t0 = time()\n",
      "    for i in range(N):\n",
      "        x = grid[i]\n",
      "        for j in range(N):\n",
      "            y = grid[j]\n",
      "            julia[i,j] = kernel(x+y*1j, c, lim)\n",
      "    return julia, time() - t0"
     ],
     "language": "python",
     "metadata": {},
     "outputs": []
    },
    {
     "cell_type": "code",
     "collapsed": false,
     "input": [
      "kwargs = dict(N=1000, bound=1.5)\n",
      "plot_julia(kwargs, compute_julia_opt)"
     ],
     "language": "python",
     "metadata": {},
     "outputs": []
    },
    {
     "cell_type": "code",
     "collapsed": false,
     "input": [],
     "language": "python",
     "metadata": {},
     "outputs": []
    }
   ],
   "metadata": {}
  }
 ]
}